#!/bin/bash

awk '
function blankLine(s) {
  for (i=1;i<=length(s);i++) {
    c = substr(s, i, 1)
    if (c != "\t" && c != " ") {
      return 0;
    }
  }
  return 1;
}

function expandTabs(s) {
  ret = ""
  cnt = 0

  for (i=1;i<=length(s);i++) {
    c = substr(s, i, 1)
    if (c == "\t") {
      first = 1
      while (first || cnt % 4 != 0) {
        ret = ret "&nbsp;"
        cnt++
        first = 0
      }
    } else if (c == " ") {
      ret = ret "&nbsp;"
      cnt++
    } else {
      ret = ret c
      cnt++
    }
  }
  return ret
}
function stylingFor(s) {
  if (length(s) == 0) {
    return "";
  } else if (substr(s, 1, 1) == " " || substr(s, 1, 1) == "\t") {
    return "";
  } else {
    return " code-comment";
  }
}
BEGIN { mode = "COPY" }
mode == "SETFILE" {
  readfrom = $0
  mode = "COPY"
  lineNumber = 1
  next
}
mode == "INSERT" {
  printf "<div class='codeblock'>\n"
  readUntil = $0
  while (1) {
    if (pushBack) {
      inputLine = pushBack
      pushBack = ""
      status = 1
    } else {
      status = getline inputLine < readfrom
    }
    if (substr(inputLine, 1, length(readUntil)) == readUntil) {
      pushBack = inputLine
      break
    }
    if (blankLine(inputLine)) {
      printf "<div class=%ccodeline%c><span class=%ccodelineno%c></span><span class=%ccodelinetext%c></span></div>\n", 34,34,34,34,34,34
    } else {
      printf "<div class=%ccodeline%c><span class=%ccodelineno%c>%d</span><span class=%ccodelinetext%s%c>%s</span></div>\n", 34,34,34,34,lineNumber, 34, stylingFor(inputLine), 34, expandTabs(inputLine)
    }
    lineNumber++
    if (status != 1) {
      close(readfrom)
      break
    }
  }
  printf "</div>\n"
  mode = "COPY"
  next
}
$0 == "%readfile" { mode = "SETFILE" ; next }
$0 == "%segment" { mode = "INSERT" ; next }
mode == "COPY" { print ; next }
{ print "INVALID MODE", mode}
' "$@"