This is the deployer script to deploy a cloudfront website to support deployer.ziniki.org

	target deployer_ziniki_org

The core of the solution is to have a bucket which has all of the files in it that we want

		ensure aws.S3.Bucket "deployer.ziniki.org" => deployer_bucket
			@teardown delete

		env "DEPLOYER_WEBSITE_DIR" => root

		files.dir root "IMAGE" => src_dir

		files.copy src_dir deployer_bucket

		ensure aws.CertificateManager.Certificate "deployer.ziniki.org" => cert
			@teardown delete
			ValidationMethod <- "DNS"
			ValidationProvider <- "dreamhost"

		cloudfront.distribution.fromS3 "for-deployer" => cloudfront
			@teardown delete
			Bucket <- deployer_bucket
			Comment <- "a distribution for deployer.ziniki.org"
			Certificate <- cert->arn
			Domain <- []
				"deployer.ziniki.org"
			MinTTL <- 300
			CacheBehaviors <- []
				{}
					PathPattern <- "*.html"
					ResponseHeaders <- {}
						Header <- "Content-Type"
						Value <- "text/html"
				{}
					PathPattern <- "*.css"
					ResponseHeaders <- {}
						Header <- "Content-Type"
						Value <- "text/css"
			TargetOriginId <- "s3-bucket-target"

		ensure dreamhost.CNAME "deployer.ziniki.org"
			@teardown delete
			PointsTo <- (cloudfront->domainName)

	target upload_deployer_content
		find aws.S3.Bucket "deployer.ziniki.org" => deployer_bucket
		find aws.CloudFront.Distribution "for-deployer" => cloudfront

		env "DEPLOYER_WEBSITE_DIR" => root

		files.dir root "IMAGE" => src_dir

		files.copy src_dir deployer_bucket

		cloudfront.invalidate cloudfront->distributionId
